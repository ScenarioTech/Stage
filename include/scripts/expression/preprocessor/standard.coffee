###
# Expression Preprocessor:
# Standard
# 
# This preprocessor selects one expression from the available expression options if there are more than one.
# Then it applies meta-templates, meta-effects and plugins.
# 
###

module.exports = (expression, params) ->

    processExpression = (exp) ->
        # each expression uses this function to set up a configuration for its effect method
        exp.setupEffect = (data, element) ->
            @effectParams =
                data: data
                element: element

        if typeof exp.template is 'function'
            exp.template = exp.template params.template.params

        # if there is a template, apply the plugins to the template, then apply the meta-template
        if params.template
            if params.template.plugins and typeof params.template.plugins.loader is 'function' and typeof params.template.plugins.hooks is 'object'
                exp.template = params.template.plugins.loader exp.template, params.template.plugins.hooks, params.template.plugins.params
                
            if params.template.meta and typeof params.template.meta.method is 'function'
                exp.template = params.template.meta.method exp.template, params.template.meta.params
        
        if params.effect

            # if there is a meta-effect, use it to wrap the effect
            # if the effect function is generated by way of a "generate" method, run it to get the effect
            if params.effect and typeof exp.effect is 'object' and typeof exp.effect.generate is 'function'

                if params.effect.plugins
                    if !params.effect.params
                        params.effect.params = {}
                    
                    if !params.effect.plugins.params
                        params.effect.plugins.params = {}

                    params.effect.plugins.params.parentParams = params.effect.params
                    params.effect.plugins.loader exp.effect, params.effect.plugins.hooks, params.effect.plugins.params

                exp.effect = exp.effect.generate params.effect.params

            if params.effect.meta and typeof params.effect.meta.method is 'function'
                exp.effect = params.effect.meta.method exp.effect, params.effect.meta.params

        exp

    templateKeys = Object.keys expression

    # if there is only one template, use it
    # a multiplexed expression - i.e. an expression with multiple possible sub-expressions - must not use the key "template" to refer to one of the sub-expressions
    # since the presence of that key is used to determine whether or not the expression is multiplexed
    if expression.template
        processExpression expression
        
    # if there is only one sub-expression for some reason, return it
    else if templateKeys.length is 1
        processExpression expression[templateKeys[0]]
    
    # if a selector function is present in the params, use it
    else if params.selector and typeof params.selector.method is 'function'
        processExpression params.selector.method expression, params.selector.params
    
    # otherwise just grab the first template from the list provided
    else
        processExpression expression[templateKeys[0]]
